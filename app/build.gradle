apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply from: 'detekt.gradle'
apply from: 'cpd.gradle'
apply from: 'jacoco.gradle'

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.2'
    defaultConfig {
        applicationId 'com.smlnskgmail.jaman.deviceinfo'
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName '1.2.1'

        testInstrumentationRunner 'com.smlnskgmail.jaman.deviceinfo.runner.AndroidJacocoTestRunner'
    }
    buildTypes {
        debug {
            testCoverageEnabled = true
        }
    }
    lintOptions {
        abortOnError false
    }
    testOptions {
        unitTests {
            all {
                jacoco {
                    includeNoLocationClasses = true
                }
            }
            includeAndroidResources = true
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def versionName = android.defaultConfig.versionName
            def buildTypeName = variant.buildType.name
            output.outputFileName = "android-device-info-${buildTypeName}_${versionName}.apk"
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'com.google.android.material:material:1.2.0-alpha05'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.viewpager2:viewpager2:1.0.0'
    implementation 'androidx.preference:preference:1.1.0'

    // Unit tests
    testImplementation 'junit:junit:4.13'

    // Android tests
    androidTestImplementation 'androidx.annotation:annotation:1.1.0'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.2.0'

    // Screengrab
    androidTestImplementation 'tools.fastlane:screengrab:2.0.0'
}
